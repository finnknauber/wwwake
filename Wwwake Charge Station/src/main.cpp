#include <Adafruit_SSD1306.h>
#include <Adafruit_GFX.h>
#include <Arduino.h>
#include <EEPROM.h>
#include <RTClib.h>
#include <Wire.h>


// Pins
#define LED PC13
#define BATLEVEL PB1
#define TX PA2
#define RX PA3
#define CHARGE PB4
#define STANDBY PB3
#define ENCODERA PA9
#define ENCODERB PA10
#define BUZZER PA8
#define ENCODERSWITCH PB15
#define CE PB13

// Screens
#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define SELECTION_PAGE -1
#define ALARM_PAGE 0
#define VIBRATION_PAGE 1
#define SETTINGS_PAGE 2

// EEPROM
#define ADDRESS_ALWAYS_ON 1
#define ADDRESS_SOUND 2
#define ADDRESS_BRIGHTNESS 3
#define ADDRESS_VIBRATION_PATTERN 4
#define ADDRESS_ALARM_TIME 5


Adafruit_SSD1306 oled(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, -1);
RTC_DS1307 rtc;


// Display Helpers
const unsigned int arrowLocations[4] = {16, 56, 96};
const unsigned int arrowLocationsSettings[4] = {6, 40, 72, 106};
String timeString = "";
int currentLowerPage = 0;
int currentPage = -1;

// Wake Data
byte wakeHour = 0;
byte wakeMinute = 0;
byte vibrationPattern = 1;

// User Input
unsigned long lastUserInput = 0;
bool pressed = false;
bool longPressRegistered = false;
bool activeUser = false;

// Settings
bool alwaysOn = true;
bool soundOn = true;
bool dimmed = false;

// Encoder
unsigned long switchLastPressed = 0;
unsigned long lastEncoderTurn = 0;
unsigned long lastEncoderCallA = 0;
unsigned long lastEncoderCallB = 0;
volatile int encoderLocation = 0;
bool allowEncoderOverflow = true;
int minLimitEncoder = 0;
int maxLimitEncoder = 2;



const unsigned char alwaysOffBitmap [] PROGMEM = {
    0x80, 0x00, 0x00, 0x00, 0xC0, 0x00, 0x00, 0x00, 0x60, 0xF0, 0x78, 0x00, 0x31, 0x08, 0x84, 0x00,
    0x19, 0x08, 0x04, 0x00, 0x0D, 0x08, 0x04, 0x00, 0x07, 0x08, 0x38, 0x00, 0x03, 0x08, 0x04, 0x00,
    0x01, 0x88, 0x04, 0x00, 0x01, 0xC8, 0x84, 0x00, 0x00, 0xF0, 0x78, 0x00, 0x00, 0x30, 0x00, 0x00,
    0x00, 0x18, 0x00, 0x00, 0x00, 0x0C, 0x00, 0x00, 0x00, 0x06, 0x00, 0x00, 0x00, 0x03, 0x00, 0x00,
    0x00, 0x01, 0x80, 0x00, 0x00, 0x00, 0xC0, 0x00, 0x00, 0x00, 0x60, 0x00, 0x00, 0x20, 0x78, 0x00,
    0x00, 0xE0, 0x9C, 0x00, 0x00, 0x20, 0x8C, 0x00, 0x00, 0x20, 0x86, 0x00, 0x00, 0x20, 0x7B, 0x00,
    0x00, 0x20, 0x85, 0x80, 0x00, 0x20, 0x84, 0xC0, 0x00, 0x20, 0x84, 0x60, 0x00, 0xF8, 0x78, 0x30,
    0x00, 0x00, 0x00, 0x18, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};


const unsigned char alwaysOnBitmap [] PROGMEM = {
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0xF0, 0x78, 0x00, 0x01, 0x08, 0x84, 0x00,
    0x01, 0x08, 0x04, 0x00, 0x01, 0x08, 0x04, 0x00, 0x01, 0x08, 0x38, 0x00, 0x01, 0x08, 0x04, 0x00,
    0x01, 0x08, 0x04, 0x00, 0x01, 0x08, 0x84, 0x00, 0x00, 0xF0, 0x78, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x20, 0x78, 0x00,
    0x00, 0xE0, 0x84, 0x00, 0x00, 0x20, 0x84, 0x00, 0x00, 0x20, 0x84, 0x00, 0x00, 0x20, 0x78, 0x00,
    0x00, 0x20, 0x84, 0x00, 0x00, 0x20, 0x84, 0x00, 0x00, 0x20, 0x84, 0x00, 0x00, 0xF8, 0x78, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};


const unsigned char brightnessOnBitmap [] PROGMEM = {
    0x00, 0x02, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00,
    0x0C, 0x07, 0x01, 0x80, 0x0E, 0x02, 0x03, 0x80, 0x07, 0x00, 0x07, 0x00, 0x03, 0x80, 0x0E, 0x00,
    0x01, 0x80, 0x0C, 0x00, 0x00, 0x0F, 0x80, 0x00, 0x00, 0x1F, 0xC0, 0x00, 0x00, 0x3F, 0xE0, 0x00,
    0x00, 0x7F, 0xF0, 0x00, 0x78, 0x7F, 0xF0, 0xF0, 0xFC, 0x7F, 0xF1, 0xF8, 0x78, 0x7F, 0xF0, 0xF0,
    0x00, 0x7F, 0xF0, 0x00, 0x00, 0x3F, 0xE0, 0x00, 0x00, 0x1F, 0xC0, 0x00, 0x00, 0x0F, 0x80, 0x00,
    0x01, 0x80, 0x0C, 0x00, 0x03, 0x80, 0x0E, 0x00, 0x07, 0x00, 0x07, 0x00, 0x0E, 0x02, 0x03, 0x80,
    0x0C, 0x07, 0x01, 0x80, 0x00, 0x07, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00,
    0x00, 0x02, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};


const unsigned char brightnessOffBitmap [] PROGMEM = {
    0x00, 0x02, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00,
    0x0C, 0x07, 0x01, 0x80, 0x0E, 0x02, 0x03, 0x80, 0x07, 0x00, 0x07, 0x00, 0x03, 0x80, 0x0E, 0x00,
    0x01, 0x80, 0x0C, 0x00, 0x00, 0x0F, 0x80, 0x00, 0x00, 0x18, 0xC0, 0x00, 0x00, 0x30, 0x60, 0x00,
    0x00, 0x60, 0x30, 0x00, 0x78, 0x40, 0x10, 0xF0, 0xFC, 0x40, 0x11, 0xF8, 0x78, 0x40, 0x10, 0xF0,
    0x00, 0x60, 0x30, 0x00, 0x00, 0x30, 0x60, 0x00, 0x00, 0x18, 0xC0, 0x00, 0x00, 0x0F, 0x80, 0x00,
    0x01, 0x80, 0x0C, 0x00, 0x03, 0x80, 0x0E, 0x00, 0x07, 0x00, 0x07, 0x00, 0x0E, 0x02, 0x03, 0x80,
    0x0C, 0x07, 0x01, 0x80, 0x00, 0x07, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00,
    0x00, 0x02, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};


const unsigned char soundOnBitmap [] PROGMEM = {
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x03, 0x03, 0x80,
    0x00, 0x07, 0x03, 0x80, 0x00, 0x0F, 0x01, 0xC0, 0x00, 0x1F, 0x09, 0xC0, 0x00, 0x3F, 0x0C, 0x60,
    0x00, 0x7F, 0x0E, 0x60, 0xFF, 0xFF, 0x66, 0x30, 0xFF, 0xFF, 0x66, 0x30, 0xFF, 0xFF, 0x33, 0x38,
    0xFF, 0xFF, 0x33, 0xB8, 0xFF, 0xFF, 0x33, 0xB8, 0xFF, 0xFF, 0x33, 0xB8, 0xFF, 0xFF, 0x33, 0xB8,
    0xFF, 0xFF, 0x33, 0x38, 0xFF, 0xFF, 0x66, 0x30, 0xFF, 0xFF, 0x66, 0x30, 0x00, 0x7F, 0x0E, 0x60,
    0x00, 0x3F, 0x0C, 0x60, 0x00, 0x1F, 0x09, 0xC0, 0x00, 0x0F, 0x01, 0xC0, 0x00, 0x07, 0x03, 0x80,
    0x00, 0x03, 0x03, 0x80, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};


const unsigned char soundOffBitmap [] PROGMEM = {
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x03, 0x00, 0x00,
    0x00, 0x07, 0x00, 0x00, 0x00, 0x0F, 0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, 0x3F, 0x00, 0x00,
    0x00, 0x7F, 0x60, 0x18, 0xFF, 0xFF, 0x70, 0x38, 0xFF, 0xFF, 0x38, 0x70, 0xFF, 0xFF, 0x1C, 0xE0,
    0xFF, 0xFF, 0x0F, 0xC0, 0xFF, 0xFF, 0x07, 0x80, 0xFF, 0xFF, 0x07, 0x80, 0xFF, 0xFF, 0x0F, 0xC0,
    0xFF, 0xFF, 0x1C, 0xE0, 0xFF, 0xFF, 0x38, 0x70, 0xFF, 0xFF, 0x70, 0x38, 0x00, 0x7F, 0x60, 0x18,
    0x00, 0x3F, 0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, 0x0F, 0x00, 0x00, 0x00, 0x07, 0x00, 0x00,
    0x00, 0x03, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};


const unsigned char clockBitmap [] PROGMEM = {
    0x00, 0x1F, 0xC0, 0x00, 0x00, 0xFF, 0xF8, 0x00, 0x03, 0xF0, 0x7E, 0x00, 0x07, 0x80, 0x0F, 0x00,
    0x0E, 0x00, 0x03, 0x80, 0x1C, 0x00, 0x01, 0xC0, 0x38, 0x02, 0x00, 0xE0, 0x30, 0x03, 0x00, 0x60,
    0x70, 0x03, 0x00, 0x70, 0x60, 0x07, 0x00, 0x30, 0x60, 0x07, 0x00, 0x30, 0xE0, 0x07, 0x00, 0x38,
    0xC0, 0x07, 0x00, 0x18, 0xC0, 0x07, 0x00, 0x18, 0xC0, 0x07, 0x80, 0x18, 0xC0, 0x03, 0xC0, 0x18,
    0xC0, 0x01, 0xE0, 0x18, 0xE0, 0x00, 0xF0, 0x38, 0x60, 0x00, 0x38, 0x30, 0x60, 0x00, 0x18, 0x30,
    0x70, 0x00, 0x00, 0x70, 0x30, 0x00, 0x00, 0x60, 0x38, 0x00, 0x00, 0xE0, 0x1C, 0x00, 0x01, 0xC0,
    0x0E, 0x00, 0x03, 0x80, 0x07, 0x80, 0x0F, 0x00, 0x03, 0xF0, 0x7E, 0x00, 0x00, 0xFF, 0xF8, 0x00,
    0x00, 0x1F, 0xC0, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};


const unsigned char settingsBitmap [] PROGMEM = {
    0x00, 0x03, 0xC0, 0x00, 0x00, 0x07, 0xE0, 0x00, 0x00, 0x07, 0xE0, 0x00, 0x00, 0x07, 0xE0, 0x00,
    0x03, 0x87, 0xE1, 0xC0, 0x07, 0xFF, 0xFF, 0xE0, 0x0F, 0xFF, 0xFF, 0xF0, 0x0F, 0xFF, 0xFF, 0xF0,
    0x0F, 0xFF, 0xFF, 0xF0, 0x07, 0xF8, 0x1F, 0xE0, 0x07, 0xE0, 0x07, 0xE0, 0x07, 0xC0, 0x03, 0xE0,
    0x07, 0xC0, 0x03, 0xE0, 0x7F, 0x80, 0x01, 0xFE, 0xFF, 0x80, 0x01, 0xFF, 0xFF, 0x80, 0x01, 0xFF,
    0xFF, 0x80, 0x01, 0xFF, 0xFF, 0x80, 0x01, 0xFF, 0x7F, 0x80, 0x01, 0xFE, 0x07, 0xC0, 0x03, 0xE0,
    0x07, 0xC0, 0x03, 0xE0, 0x07, 0xE0, 0x07, 0xE0, 0x07, 0xF8, 0x1F, 0xE0, 0x0F, 0xFF, 0xFF, 0xF0,
    0x0F, 0xFF, 0xFF, 0xF0, 0x0F, 0xFF, 0xFF, 0xF0, 0x07, 0xFF, 0xFF, 0xE0, 0x03, 0x87, 0xE1, 0xC0,
    0x00, 0x07, 0xE0, 0x00, 0x00, 0x07, 0xE0, 0x00, 0x00, 0x07, 0xE0, 0x00, 0x00, 0x03, 0xC0, 0x00
};


const unsigned char alarmBitmap [] PROGMEM = {
    0x07, 0x00, 0x00, 0x60, 0x1F, 0xC0, 0x03, 0xF8, 0x3F, 0x80, 0x01, 0xFE, 0x7F, 0x00, 0x00, 0xFE,
    0xFE, 0x07, 0xE0, 0x7F, 0xFC, 0x3F, 0xFC, 0x3F, 0xF0, 0xFF, 0xFF, 0x0F, 0xE1, 0xFC, 0x1F, 0x87,
    0x43, 0xE0, 0x07, 0xC2, 0x07, 0x80, 0x01, 0xE0, 0x07, 0x01, 0x80, 0xF0, 0x0E, 0x01, 0x80, 0x70,
    0x1E, 0x01, 0xC0, 0x78, 0x1C, 0x01, 0xC0, 0x38, 0x1C, 0x01, 0xC0, 0x38, 0x38, 0x01, 0xC0, 0x1C,
    0x38, 0x01, 0xC0, 0x1C, 0x38, 0x01, 0xC0, 0x1C, 0x38, 0x01, 0xE0, 0x1C, 0x38, 0x01, 0xF0, 0x1C,
    0x38, 0x00, 0xF0, 0x1C, 0x1C, 0x00, 0x70, 0x38, 0x1C, 0x00, 0x00, 0x38, 0x1E, 0x00, 0x00, 0x78,
    0x0E, 0x00, 0x00, 0x70, 0x0F, 0x00, 0x00, 0xF0, 0x07, 0x80, 0x01, 0xE0, 0x07, 0xE0, 0x07, 0xF0,
    0x0F, 0xF8, 0x1F, 0xF8, 0x1E, 0xFF, 0xFF, 0x78, 0x3C, 0x3F, 0xFC, 0x3C, 0x18, 0x07, 0xE0, 0x18
};


const unsigned char vibrationBitmap [] PROGMEM = {
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x01, 0xFF, 0xFF, 0x00, 0x01, 0xFF, 0xFF, 0x00, 0x01, 0x80, 0x03, 0x00, 0x21, 0x80, 0x03, 0x04,
    0x71, 0x80, 0x03, 0x0E, 0x39, 0x80, 0x03, 0x1C, 0x39, 0x80, 0x03, 0x1C, 0x71, 0x80, 0x03, 0x0E,
    0xE1, 0x80, 0x03, 0x07, 0x71, 0x80, 0x03, 0x0E, 0x39, 0x80, 0x03, 0x1C, 0x71, 0x80, 0x03, 0x0E,
    0xE1, 0x80, 0x03, 0x07, 0x71, 0x80, 0x03, 0x0E, 0x39, 0x80, 0x03, 0x1C, 0x31, 0x80, 0x03, 0x0C,
    0x61, 0x80, 0x03, 0x06, 0x01, 0x80, 0x03, 0x00, 0x01, 0x80, 0x03, 0x00, 0x01, 0x80, 0x03, 0x00,
    0x01, 0x80, 0x03, 0x00, 0x01, 0x80, 0x03, 0x00, 0x01, 0x80, 0x03, 0x00, 0x01, 0xFF, 0xFF, 0x00,
    0x01, 0xFF, 0xFF, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};


const unsigned char arrowBitmap [] PROGMEM = {
    0x01, 0x80, 0x01, 0x80, 0x01, 0x80, 0x01, 0x80, 0x01, 0x80, 0x01, 0x80, 0x01, 0x80, 0x01, 0x80,
    0x31, 0x8C, 0x39, 0x9C, 0x1D, 0xB8, 0x0F, 0xF0, 0x07, 0xE0, 0x01, 0x80,
};

const unsigned char wwwakeBitmap [] PROGMEM = {
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, 0x00,
    0x01, 0xF0, 0xF0, 0x7B, 0xC1, 0xE1, 0xF7, 0x87, 0xC3, 0xE3, 0xF8, 0x1F, 0x0F, 0x83, 0xF8, 0x00,
    0x00, 0xF1, 0xF8, 0xFB, 0xC3, 0xE1, 0xE7, 0x87, 0xC3, 0xC7, 0xFE, 0x1F, 0x1F, 0x0F, 0xFC, 0x00,
    0x00, 0xF1, 0xF8, 0xF3, 0xC3, 0xE1, 0xE7, 0x8F, 0xC7, 0xCF, 0xFE, 0x1F, 0x3E, 0x0F, 0xFE, 0x00,
    0x00, 0xF1, 0xF9, 0xF1, 0xE7, 0xE3, 0xC7, 0x8F, 0xC7, 0x8F, 0x1F, 0x1F, 0x7C, 0x1F, 0x1F, 0x00,
    0x00, 0xF3, 0xF9, 0xE1, 0xE7, 0xF3, 0xC7, 0x8F, 0xCF, 0x8E, 0x0F, 0x1F, 0xF8, 0x3E, 0x0F, 0x00,
    0x00, 0xF3, 0xF9, 0xE1, 0xEF, 0xF7, 0x83, 0x9F, 0xCF, 0x00, 0x7F, 0x1F, 0xF8, 0x3E, 0x0F, 0x00,
    0x00, 0xF7, 0xBB, 0xC1, 0xEE, 0xF7, 0x83, 0xDD, 0xCF, 0x07, 0xFF, 0x1F, 0xF8, 0x3F, 0xFF, 0x00,
    0x00, 0x77, 0x3B, 0xC1, 0xEE, 0x7F, 0x03, 0xFD, 0xFE, 0x0F, 0xFF, 0x1F, 0xFC, 0x3F, 0xFF, 0x00,
    0x00, 0x7F, 0x3F, 0x81, 0xFE, 0x7F, 0x03, 0xF9, 0xFE, 0x1F, 0x8F, 0x1F, 0xFC, 0x3C, 0x00, 0x00,
    0x00, 0x7F, 0x3F, 0x80, 0xFC, 0x7E, 0x03, 0xF9, 0xFC, 0x1F, 0x0F, 0x1F, 0xBE, 0x3E, 0x0F, 0x00,
    0x00, 0x7E, 0x3F, 0x00, 0xFC, 0x7E, 0x03, 0xF0, 0xFC, 0x1F, 0x1F, 0x1F, 0x3E, 0x1F, 0x1F, 0x00,
    0x00, 0x7E, 0x3F, 0x00, 0xF8, 0x7C, 0x01, 0xF0, 0xF8, 0x1F, 0xFF, 0x1F, 0x1F, 0x0F, 0xFE, 0x00,
    0x00, 0x7C, 0x1E, 0x00, 0xF8, 0x7C, 0x01, 0xF0, 0xF8, 0x0F, 0xFF, 0x1F, 0x0F, 0x8F, 0xFE, 0x00,
    0x00, 0x7C, 0x1E, 0x00, 0xF8, 0x78, 0x01, 0xE0, 0xF0, 0x07, 0xEF, 0x1F, 0x0F, 0x83, 0xF8, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x04, 0x44, 0x02, 0x10, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x04, 0x64, 0x06, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x06, 0xE4, 0x46, 0x00, 0xC3, 0x40, 0x12, 0x80, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x02, 0xAC, 0xA6, 0x9B, 0xE6, 0xC1, 0x13, 0x60, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x07, 0x02, 0xB8, 0x77, 0x1B, 0x24, 0x41, 0x12, 0x20, 0xE0, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x03, 0x99, 0xB7, 0x9B, 0x24, 0xC1, 0x92, 0x20, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x01, 0x19, 0xB6, 0x9B, 0x27, 0xC0, 0xF3, 0xE0, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x01, 0x00, 0xC0, 0x00, 0x20, 0x40, 0x02, 0x80, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x06, 0x80, 0x02, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x03, 0x00, 0x02, 0x00, 0x00, 0x00, 0x00, 0x00
};



void playErrorSound() {
    if (soundOn) {
        tone(BUZZER, 4000);
        delay(500);
        tone(BUZZER, 1000);
        delay(700);
        noTone(BUZZER);
        digitalWrite(BUZZER, LOW);
    }
}


void playShortPress() {
    if (soundOn) {
        tone(BUZZER, 4000);
        delay(50);
        noTone(BUZZER);
        digitalWrite(BUZZER, LOW);
    }
}


void playLongPress() {
    if (soundOn) {
        tone(BUZZER, 4000);
        delay(250);
        noTone(BUZZER);
        digitalWrite(BUZZER, LOW);
    }
}


void playStartSound() {
    if (soundOn) {
        tone(BUZZER, 2500);
        delay(500);
        tone(BUZZER, 3000);
        delay(500);
        tone(BUZZER, 4000);
        delay(500);
        noTone(BUZZER);
        digitalWrite(BUZZER, LOW);
    }
}


void getEEPROM() {
    EEPROM.get(ADDRESS_ALWAYS_ON, alwaysOn);
    EEPROM.get(ADDRESS_BRIGHTNESS, dimmed);
    EEPROM.get(ADDRESS_ALARM_TIME, wakeHour);
    EEPROM.get(ADDRESS_ALARM_TIME+1, wakeMinute);
    EEPROM.get(ADDRESS_VIBRATION_PATTERN, vibrationPattern);
    EEPROM.get(ADDRESS_SOUND, soundOn);
    delay(50);
}


void updateEEPROM() {
    EEPROM.update(ADDRESS_ALWAYS_ON, alwaysOn);
    EEPROM.update(ADDRESS_SOUND, soundOn);
    EEPROM.update(ADDRESS_BRIGHTNESS, dimmed);
    EEPROM.update(ADDRESS_ALARM_TIME, wakeHour);
    EEPROM.update(ADDRESS_ALARM_TIME+1, wakeMinute);
    EEPROM.update(ADDRESS_VIBRATION_PATTERN, vibrationPattern);
    delay(10);
}


void firstProgram(bool is_first) {
    if (is_first) {
        rtc.adjust(DateTime(F(__DATE__), F(__TIME__)) + TimeSpan(6));
        EEPROM.update(ADDRESS_ALWAYS_ON, 1);
        EEPROM.update(ADDRESS_SOUND, 1);
        EEPROM.update(ADDRESS_BRIGHTNESS, 0);
        EEPROM.update(ADDRESS_ALARM_TIME, 0);
        EEPROM.update(ADDRESS_ALARM_TIME+1, 0);
        EEPROM.update(ADDRESS_VIBRATION_PATTERN, 1);
        delay(5);
        getEEPROM();
        while(true) {}
    }
}


int limitEncoder() {
    if (encoderLocation < minLimitEncoder) {
        if (allowEncoderOverflow) {
            encoderLocation = maxLimitEncoder;
        }
        else {
            encoderLocation = minLimitEncoder;
        }
    }
    if (encoderLocation > maxLimitEncoder) {
        if (allowEncoderOverflow) {
            encoderLocation = minLimitEncoder;
        }
        else {
            encoderLocation = maxLimitEncoder;
        }
    }
    return encoderLocation;
}


void displayClock(bool center) {
    DateTime now = rtc.now();
    timeString = "";
    if (now.hour() < 10) {
        timeString += "0";
    }
    timeString += now.hour();

    if (now.second() % 2 == 1) {
        timeString += " ";
    }
    else {
        timeString += ":";
    }


    if (now.minute() < 10) {
        timeString += "0";
    }
    timeString += now.minute();

    if (center) {
        oled.setTextSize(3);
        oled.setCursor((SCREEN_WIDTH - 90) / 2, (SCREEN_HEIGHT - 24) / 2);
    }
    else {
        oled.setTextSize(2);
        oled.setCursor((SCREEN_WIDTH - 60) / 2, 0);
    }
    oled.println(timeString);

    delay(3);
}


void displaySettings() {
    oled.setTextSize(2);
    minLimitEncoder = 0; maxLimitEncoder = 3; allowEncoderOverflow = true;

    switch (encoderLocation) {
        case 0:
            oled.setCursor(15, 0);
            oled.print("Always On");
            break;
        case 1:
            oled.setCursor(8, 0);
            oled.print("Sound/Beep");
            break;
        case 2:
            oled.setCursor(8, 0);
            oled.print("Brightness");
            break;
        case 3:
            oled.setCursor(18, 0);
            oled.print("Set Time");
            break;
    }

    oled.drawBitmap(arrowLocationsSettings[encoderLocation], 16, arrowBitmap, 16, 14, WHITE);

    if (alwaysOn) {
        oled.drawBitmap(0, 34, alwaysOnBitmap, 32, 32, WHITE);
    }
    else {
        oled.drawBitmap(0, 34, alwaysOffBitmap, 32, 32, WHITE);
    }
    if (soundOn) {
        oled.drawBitmap(33, 34, soundOnBitmap, 32, 32, WHITE);
    }
    else {
        oled.drawBitmap(33, 34, soundOffBitmap, 32, 32, WHITE);
    }
    if (!dimmed) {
        oled.drawBitmap(66, 34, brightnessOnBitmap, 32, 32, WHITE);
    }
    else {
        oled.drawBitmap(66, 34, brightnessOffBitmap, 32, 32, WHITE);
    }
    oled.drawBitmap(99, 34, clockBitmap, 32, 32, WHITE);
}


void displayAlarm() {
    oled.setCursor(22, 0);
    oled.println("Set Alarm Time");
    oled.setTextSize(3);
    oled.setCursor((SCREEN_WIDTH - 90) / 2, (SCREEN_HEIGHT - 24) / 2);
    if (currentLowerPage == 0) {
        minLimitEncoder = 0; maxLimitEncoder = 23; allowEncoderOverflow = true;
        int currentLocation = encoderLocation;
        wakeHour = currentLocation;
        if (currentLocation < 10) {
            oled.print("0");
        }
        oled.print(currentLocation);
        oled.print(":");
        if (wakeMinute < 10) {
            oled.print("0");
        }
        oled.println(wakeMinute);
    }
    else {
        if (wakeHour < 10) {
            oled.print("0");
        }
        oled.print(wakeHour);
        oled.print(":");
        minLimitEncoder = 0; maxLimitEncoder = 59; allowEncoderOverflow = true;
        int currentLocation = encoderLocation;
        wakeMinute = currentLocation;
        if (currentLocation < 10) {
            oled.print("0");
        }
        oled.println(currentLocation);
    }
}


void displayVibration() {
    oled.setCursor(1, 0);
    oled.println("Set Vibration Pattern");
    oled.setTextSize(4);
    oled.setCursor(54, 24);
    minLimitEncoder = 0; maxLimitEncoder = 4; allowEncoderOverflow = false;
    oled.println(encoderLocation+1);
    vibrationPattern = encoderLocation+1;
}


void displayPages() {
    oled.setTextSize(1);
    switch (currentPage) {
        case ALARM_PAGE:
            displayAlarm();
            break;
        case VIBRATION_PAGE:
            displayVibration();
            break;
        case SETTINGS_PAGE:
            displaySettings();
            break;
        case SELECTION_PAGE:
            displayClock(false);
            minLimitEncoder = 0; maxLimitEncoder = 2; allowEncoderOverflow = true;
            oled.drawBitmap(arrowLocations[encoderLocation], 16, arrowBitmap, 16, 14, WHITE);
            // draw Settings icon
            oled.drawBitmap(8, 32, alarmBitmap, 32, 32, WHITE);
            // draw Alarm icon
            oled.drawBitmap(48, 32, vibrationBitmap, 32, 32, WHITE);
            // draw vibration Pattern icon
            oled.drawBitmap(88, 32, settingsBitmap, 32, 32, WHITE);
            break;
        default:
            currentPage = SELECTION_PAGE;
            break;
    }
}


void checkActive() {
    if (millis() - lastUserInput < 15000) {
        if (millis() > 15000 or lastUserInput != 0) {
            if (not activeUser) {
                activeUser = true;
                encoderLocation = 0;
            }
        }
    }
    else {
        if (activeUser) {
            currentPage = SELECTION_PAGE;
            activeUser = false;
            updateEEPROM();
        }
    }
}


void handleShortPress() {
    switch (currentPage) {
        case SELECTION_PAGE:
            currentLowerPage = 0;
            currentPage = encoderLocation;
            encoderLocation = 0;
            switch (currentPage) {
                case ALARM_PAGE:
                    encoderLocation = wakeHour;
                    break;

                case VIBRATION_PAGE:
                    encoderLocation = vibrationPattern-1;
                    break;

                case SETTINGS_PAGE:
                    encoderLocation = 0;
                    break;
                
                default:
                    currentPage = SELECTION_PAGE;
                    break;
            }
            break;

        case ALARM_PAGE:
            if (currentLowerPage == 0) {
                encoderLocation = wakeMinute;
                currentLowerPage = 1;
            }
            else {
                encoderLocation = 0;
                currentLowerPage = 0;
                currentPage = SELECTION_PAGE;
            }
            break;

        case VIBRATION_PAGE:
            encoderLocation = 0;
            currentLowerPage = 0;
            currentPage = SELECTION_PAGE;
            break;

        case SETTINGS_PAGE:
            if (currentLowerPage == 0) {
                if (encoderLocation == 0) {
                    alwaysOn = !alwaysOn;
                }
                else if (encoderLocation == 1) {
                    soundOn = !soundOn;
                }
                else if (encoderLocation == 2) {
                    dimmed = !dimmed;
                    oled.dim(dimmed);
                }
            }
            else {
                encoderLocation = 0;
                currentLowerPage = 0;
                currentPage = SELECTION_PAGE;
            }
            break;

        default:
            currentPage = SELECTION_PAGE;
            break;
    }
}


void checkEncoderButton() {
    if (digitalRead(ENCODERSWITCH) == LOW) {
        if (not pressed) {
            switchLastPressed = millis();
            pressed = true;
        }
        else if (millis() - switchLastPressed > 500 and not longPressRegistered) {
            longPressRegistered = true;
            playLongPress();
            lastUserInput = millis();
            if (activeUser and currentPage == SELECTION_PAGE) {
                activeUser = false;
                lastUserInput = 0;
            }
            else if (activeUser and currentPage != SELECTION_PAGE) {
                updateEEPROM();
                currentPage = SELECTION_PAGE;
                encoderLocation = 0;
                currentLowerPage = 0;
            }
        }
    }

    else if (digitalRead(ENCODERSWITCH) == HIGH) {
        if (pressed) {
            pressed = false;
            longPressRegistered = false;
            if (millis() - switchLastPressed <= 500) {
                lastUserInput = millis();
                if (activeUser) {
                    handleShortPress();
                }
                playShortPress();
                digitalWrite(CE, !digitalRead(CE));
            }
        }
    }
}


void encoder_turn_callA() {
    if (millis() - lastEncoderCallA > 15) {
        if (digitalRead(ENCODERA) == LOW and digitalRead(ENCODERB) == LOW) {
            if (millis() - lastEncoderTurn > 25) {
                encoderLocation++;
                limitEncoder();
                lastUserInput = millis();
                lastEncoderTurn = millis();
            }
        }
        lastEncoderCallA = millis();
    }
}


void encoder_turn_callB() {
    if (millis() - lastEncoderCallB > 15) {
        if (digitalRead(ENCODERA) == LOW and digitalRead(ENCODERB) == LOW) {
            if (millis() - lastEncoderTurn > 25) {
                encoderLocation--;
                limitEncoder();
                lastUserInput = millis();
                lastEncoderTurn = millis();
            }
        }
        lastEncoderCallB = millis();
    }
}




void setup() {
    timeString.reserve(15);
    analogReadResolution(12);

    pinMode(CHARGE, INPUT_PULLUP);
    pinMode(STANDBY, INPUT_PULLUP);
    pinMode(ENCODERSWITCH, INPUT);
    pinMode(ENCODERA, INPUT);
    pinMode(ENCODERB, INPUT);
    pinMode(BUZZER, OUTPUT);
    pinMode(LED, OUTPUT);
    pinMode(CE, OUTPUT);
    digitalWrite(CE, LOW);
    delay(10);
    getEEPROM();


    // Initialize OLED Screen
    if (!oled.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {
        playErrorSound();
        // TODO Error handling
        delay(100);
        while (true);
    }
    oled.dim(dimmed);
    delay(10);

    // Display Start Screen
    oled.clearDisplay();
    oled.setTextColor(WHITE);
    oled.setTextSize(2);
    oled.drawBitmap(0, 15, wwwakeBitmap, 128, 35, WHITE);
    oled.display();
    playStartSound();
    digitalWrite(LED, LOW);
    delay(500);
    digitalWrite(LED, HIGH);

    // SETUP RTC MODULE
    if (!rtc.begin()) {
        playErrorSound();
        // TODO Error handling
        delay(100);
        while (true);
    }
    firstProgram(false);
    delay(500);

    attachInterrupt(digitalPinToInterrupt(ENCODERA), encoder_turn_callA, FALLING);
    attachInterrupt(digitalPinToInterrupt(ENCODERB), encoder_turn_callB, FALLING);
    delay(250);
}


void loop() {
    /*oled.clearDisplay();
    oled.setCursor(0, 0);
    oled.print("CHARGE: ");
    oled.println(!digitalRead(CHARGE));
    oled.print("STANDBY: ");
    oled.println(!digitalRead(STANDBY));
    oled.println("Bat-Level");
    delay(30);
    float value = 0.0;
    for (int i=0; i<10; i++) {
        value = value + (float(analogRead(BATLEVEL))/4096) * 3.3 * 2;
        delay(5);
    }
    float median = (float(value)/10);
    oled.println(median);
    oled.display();
    delay(50);*/

    checkEncoderButton();
    checkActive();


    // draw Interface
    oled.clearDisplay();
    if (activeUser) {
        displayPages();
        delay(5);
    }
    else if (alwaysOn) {
        displayClock(true);
    }


    oled.display();
    delay(10);
}

